#!/usr/bin/env perl

use UUID::Tiny;
use Modern::Perl;
use Mojolicious::Lite;

use Tryperl5::Context;
#use Tryperl5::Excersises;

get '/' => sub {
    my $self = shift;

    my $cid = $self->session('cid');
    if(not defined get_context($cid)) {
        $cid = create_UUID_as_string(UUID_V4);
        $self->session(cid => $cid);
        add_context($cid);
    }
    $self->stash(perl_version => sub { chomp(my @v = `$^X -v`); $v[1]; }->());
} => 'index';

post '/eval' => sub {
    my $self = shift;

    my $cid = $self->session('cid');
    unless(defined $cid) {
        return $self->render_json({});
    }

    my($out, $err, $ret) = safe_eval($self->param('code'), $cid);
    $self->render_json({
        stdout  => $out,
        stderr  => $err,
        retval  => $ret,
    });
};

get '/reset' => sub {
    my $self = shift;

    my $cid = $self->session('cid');
    if(defined $cid) {
        $self->session(cid => 0);
        remove_context($cid);
    }
    
    $self->redirect_to('index');
};

app->static(
    MojoX::Dispatcher::Static->new(
        prefix => '/js',
        root   => app->home->rel_dir('jquery-console'),
    )
);
app->secret('tryperl5 secret');
app->start;

__DATA__
@@ index.html.ep
<!doctype html>
<html>
    <head>
        <title>Try Perl 5! An interactive tutorial in your browser</title>
        <script src="/js/jquery-1.4.2.min.js" type="text/javascript"></script>
        <script src="/js/jquery.console.js" type="text/javascript"></script>
        <script type="text/javascript">
            $(document).ready( function() {
                var console = $('<div class="console"/>');
                $('body').append(console);
                console.console({
                    autofocus: true,
                    promptHistory: true,
                    promtLabel: '> ',
                    welcomeMessage: '<%= $perl_version  %>',
                    commandValidate: function(line) {
                        if(line == '') return false;
                        else return true;
                    },
                    commandHandle: function(line) {
                        var data;
                        $.ajax( {
                            type: 'POST',
                            url: '/eval',
                            data: { code: line },
                            dataType: 'json',
                            async: false,
                            success: function(res) {
                                data = [
                                    { msg: res.stdout, className: 'stdout' },
                                    { msg: res.stderr, className: 'stderr' },
                                    { msg: res.retval, className: 'retval' }
                                ];
                            },
                            error: function() {
                                data = [
                                    { msg: '',              className: 'stdout' },
                                    { msg: 'request error', className: 'stderr' },
                                    { msg: '',              className: 'retval' }
                                ];
                            }
                        } );
                        return data;
                    }
                });
            });
        </script>
        <style type="text/css">
              div.console { font-size: 14px; }
              div.console div.jquery-console-inner { width:900px; height:200px; background:#333; padding:0.5em; overflow:auto; }
              div.console div.jquery-console-prompt-box { color:#fff; font-family:monospace; }
              div.console div.jquery-console-focus span.jquery-console-cursor { background:#fefefe; color:#333; font-weight:bold; }
              div.console div.stderr { color:#ef0505; font-family:monospace; padding:0.1em; }
              div.console div.stdout { color:#1ad027; font-family:monospace; padding:0.1em; }
              div.console div.retval { color:#52666f; font-family:monospace; padding:0.1em; }
              div.console span.jquery-console-prompt-label { font-weight:bold; }
              div.console div.jquery-console-welcome { color: #fff; }
        </style>
    </head>
    <body>
        <a href="/reset">Reset</a>
    </body>
</html>
