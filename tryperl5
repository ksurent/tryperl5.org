#!/usr/bin/env perl

use Safe;
use UUID::Tiny;
use Modern::Perl;
use Data::Dumper;
use Mojolicious::Lite;
use Lexical::Persistence;
use IO::CaptureOutput qw(capture);

my %persist;

my $safe = Safe->new;
$safe->permit_only(qw(
    :base_core
    :base_mem
    :base_loop
    :base_math
    :base_orig
    say
    print
));

get '/' => sub {
    my $self = shift;

    my $sid = $self->req->cookie('sid');
    $sid = $sid->value if defined $sid;
    if(not defined $sid or not exists $persist{$sid}) {
        $sid = create_UUID_as_string(UUID_V4);
        $self->res->cookies(
            Mojo::Cookie::Response->new(
                name   => 'sid',
                value  => $sid,
                domain => 'tryperl5.org',
                path   => '/',
            )
        );
        $persist{$sid} = Lexical::Persistence->new;
    }
} => 'index';

post '/eval' => sub {
    my $self = shift;

    my $sid = $self->req->cookie('sid');
    unless(defined $sid) {
        $self->render_json({});
        return;
    }

    my($out, $err, $ret) = safe_eval($self->param('code'), $sid->value);
    $self->render_json({
        stdout  => $out,
        stderr  => $err,
        retcode => Dumper($ret),
    });
};

app->start;

sub safe_eval {
    my($code, $sid) = @_;

    local $SIG{ALRM} = sub { die 'Operation timed out' };
    alarm(3);
    my ($out, $err, $ret)= execute(compile($code, $sid),  $sid);
    alarm(0);

    ($out, $err, $ret);
}

sub compile {
    my $compiled = $safe->reval(prepare_code(@_));
    return $compiled ? $compiled : $@;
}

sub execute {
    my($compiled, $sid) = @_;

    my(@ret, $stdout, $stderr);
    unless(ref $compiled) {
        $stderr = $compiled;
        return ($stdout, $stderr, \@ret);
    }

    my $persisted = $persist{$sid}->wrap($compiled);
    capture { @ret = eval { $persisted->() } } \$stdout, \$stderr;

    ($stdout, $stderr, \@ret);
}

sub prepare_code {
    my($code, $sid) = @_;

    my $saved = join '', map { "my $_;" } keys %{ $persist{$sid}->get_context('_') };
    $saved .= $code;

    qq!sub { $saved }!;
}

__DATA__

@@ index.html.ep
% layout 'default';
<input type="text" name="code" id="code"/>
<input type="submit" id="submit"/>
<div id="result"></div>

@@ layouts/default.html.ep
<!doctype html>
<html>
    <head>
        <title>Try Perl 5! An interactive tutorial in your browser</title>
        <script src="http://code.jquery.com/jquery-1.4.2.min.js" type="text/javascript"></script>
        <script type="text/javascript">
            $(document).ready(
                function() {
                    $('#submit').click(
                        function() {
                            $.post(
                                '/eval',
                                {
                                    code: $('#code').val(),
                                },
                                function(data) {
                                    $('#result').html('Out: ' + data.stdout + "<br/>Error: " + data.stderr + "<br/>Returned: " + data.retcode);
                                },
                                'json'
                            )
                        }
                    )
                }
            );
        </script>
    </head>
    <body>
        <%== content %>
    </body>
</html>
